/* OUTPUT_FORMAT("elf32-h8300") */
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
	/* internal ROM (512KB) */
	romall(rx)	: o = 0x000000, l = 0x080000 /* 512KB */
	vectors(r)	: o = 0x000000, l = 0x000100 /* top of ROM */
	rom(rx)		: o = 0x000100, l = 0x07ff00

	/* DRAM (2MB) */
	dramall(rwx)	: o = 0x400000, l = 0x200000 /* 2MB */
	buffer(rwx)	: o = 0x500000, l = 0x100000 /* 1MB */

	/* internal RAM (16KB) */
	ramall(rwx)	: o = 0xffbf20, l = 0x004000 /* 16KB */
	softvec(rw)	: o = 0xffbf20, l = 0x000040 /* top of RAM */
	data(rwx)	: o = 0xffc000, l = 0x003f00
	bootstack(rw)	: o = 0xffff00, l = 0x000000
	intrstack(rw)	: o = 0xffff00, l = 0x000000 /* end of RAM */
}

SECTIONS
{
	.vectors : {
		vector.o(.data)
	} > vectors

	.text : {
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > rom

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		. = ALIGN(4);
		_erodata = . ;
	} > rom

	.firmdata : {
		_firmdata_start = . ;
		/* firmdata.o(.data) */
		*(.firmdata)
		_efirmdata = . ;
	} > rom

	.romdata : {
		_romdata_start = . ;
	} > rom

	.softvec : {
		_softvec = .;
	} > softvec

	.buffer : {
		_buffer_start = . ;
	} > buffer

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > data AT> rom

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > data AT> rom

	. = ALIGN(4);
	_end = . ;

	.bootstack : {
		_bootstack = .;
	} > bootstack

	.intrstack : {
		_intrstack = .;
	} > intrstack
}
